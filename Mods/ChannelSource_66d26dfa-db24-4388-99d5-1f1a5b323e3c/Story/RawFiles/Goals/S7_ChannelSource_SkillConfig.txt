Version 1
SubGoalCombiner SGC_AND
INITSECTION
//  =============================================================================================================================================
//  |                                                             SKILL-CONFIGURATOR                                                            |
//  =============================================================================================================================================

//  =================================================================================================================
//  DB_S7_Config_ModRegistry((STRING)_ModName,      (STRING)_ProjectUUID,                         (STRING)_FlagName);
    //DB_S7_Config_ModRegistry("S7_ChannelSource",    "66d26dfa-db24-4388-99d5-1f1a5b323e3c",     "S7_CS_SkillConfig");
//  =================================================================================================================

//  =============================================================================================================================================
//  DB_S7_ChannelSource_SkillConfig((STRING)_SKILLNAME);
    DB_S7_ChannelSource_SkillConfig("Shout_S7_CS_ChannelSource_I");
    DB_S7_ChannelSource_SkillConfig("Shout_S7_CS_ChannelSource_II");
    DB_S7_ChannelSource_SkillConfig("Shout_S7_CS_ChannelSource_III");
    DB_S7_ChannelSource_SkillConfig("Shout_S7_CS_ChannelSource_IV");
    DB_S7_ChannelSource_SkillConfig("Target_S7_CS_SiphonSource_I");
//  DB_S7_ChannelSource_AttributeConfig((STRING)_ATTRIBUTENAME);
    DB_S7_ChannelSource_AttributeConfig("ActionPoints");
    DB_S7_ChannelSource_AttributeConfig("Cooldown");
    DB_S7_ChannelSource_AttributeConfig("TargetRadius");
    DB_S7_ChannelSource_AttributeConfig("AreaRadius");
    DB_S7_ChannelSource_AttributeConfig("Memory Cost");
    DB_S7_ChannelSource_AttributeConfig("Magic Cost");
//  ==============================================================================================================================================

KBSECTION

//  ======================
//      REGISTER MOD
//  ======================

IF
GameStarted(_, _)                                                       //  If gameStarted
AND
NOT DB_S7_Config_ModRegistry("S7_ChannelSource",    "66d26dfa-db24-4388-99d5-1f1a5b323e3c",     "S7_CS_SkillConfig")   //  Mod not registered to StatsConfigurator
THEN
DB_S7_Config_ModRegistry("S7_ChannelSource",    "66d26dfa-db24-4388-99d5-1f1a5b323e3c",     "S7_CS_SkillConfig");      //  Register Mod
DB_S7_Config_QuickMenuRelay("S7_CS_SkillConfig");

IF
GlobalFlagSet("S7_ConfigActive")
AND
DB_S7_ChannelSource_SkillConfig(_SkillName)
AND
DB_S7_ChannelSource_AttributeConfig(_AttributeName)
AND
NOT DB_S7_Config_ModInterface("S7_ChannelSource", _SkillName, _AttributeName)
THEN
DB_S7_Config_ModInterface("S7_ChannelSource", _SkillName, _AttributeName);

//  ======================
//          TOGGLES
//  ======================

IF
GlobalFlagSet("S7_CS_MutualCooldown")
THEN
DialogSetVariableFixedString("S7_ChannelSource_ModMenu", "S7CSMutualCooldown_b0787502-bf55-467b-b935-14f0c08c1907", "Enabled.");

IF
GlobalFlagCleared("S7_CS_MutualCooldown")
THEN
DialogSetVariableFixedString("S7_ChannelSource_ModMenu", "S7CSMutualCooldown_b0787502-bf55-467b-b935-14f0c08c1907", "Disabled.");

//  ==========================
//REGION  MUTUAL-COOLDOWN

IF
CharacterUsedSkill(_Character, _SkillCast, _, _)                                        //  If character used a channel-source skill.
AND
GlobalGetFlag("S7_CS_MutualCooldown", 1)                                                //  And Mutual-Cooldown option is enabled.
AND
DB_IsPlayer(_Character)
AND
DB_S7_ChannelSource_SkillConfig(_SkillCast)                                             //  Fetch Skillnames from DB.
THEN
Proc_S7_ChannelSource_MutualCooldown(_Character, _SkillCast);                           //  Mutual-Cooldowns Procedure call.

PROC
Proc_S7_ChannelSource_MutualCooldown((CHARACTERGUID)_Character, (STRING)_SkillCast)     //  Mutual-Cooldown Procedure definition.
AND
DB_S7_ChannelSource_SkillConfig(_SkillNotCast)                                          //  Fetches all skillnames from DB.
AND
_SkillCast != _SkillNotCast                                                             //  Filters out the skill that was cast. It will be put on cooldown anyway. Probably not necessary.
AND
NRD_StatGetInt(_SkillNotCast, "Cooldown", _Cooldown)                                    //  Gets the Cooldown of the remaining skills.
AND
IntegerProduct(_Cooldown, 6, _CooldownInSeconds)                                        //  Converts Number-of-turns to time-in-seconds.
AND
Real(_CooldownInSeconds, _CooldownReal)                                                 //  Casts the value as a real-number instead of integer. cuz osiris.
THEN
DB_S7_CooldownUIUpdate(_Character, _SkillNotCast, _CooldownReal, "MutualCooldownUI");   //  Stores Information in a DB
NRD_SkillSetCooldown(_Character, _SkillNotCast, 0.0);                                   //  Set all skills off cooldown momentarily
ProcObjectTimer(_Character, "MutualCooldownUI", 10);                                    //  Start Refresh Timer

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "MutualCooldownUI")                  //  Timer finished
AND
DB_S7_CooldownUIUpdate(_Character, _SkillNotCast, _CooldownReal, "MutualCooldownUI")    //  Fetches data from DB
THEN
NRD_SkillSetCooldown(_Character, _SkillNotCast, _CooldownReal);                             //  Puts all remaining skills in cooldown.
NOT DB_S7_CooldownUIUpdate(_Character, _SkillNotCast, _CooldownReal, "MutualCooldownUI");   //  Clears DB

//END_REGION
//  ==========================

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "S7__ChannelSource_Initializer"
